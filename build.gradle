/*
 * Copyright 2017 Kaidan Gustave
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
class Version {
    String major, minor, build

    @Override
    String toString() {
        "${major}.${minor}.${this.build}"
    }
}

buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories.mavenCentral()

    dependencies { classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" }
}

plugins {
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'java'
apply plugin: 'kotlin'

group = 'me.kgustave'
version = new Version(major: 0, minor: 4, build: 9)
mainClassName = "${group}.${rootProject.name.toLowerCase()}.${rootProject.name}Kt"

def dirChar = File.separatorChar
def jdaVersion = '3.2.0_239'
def jdaUtilsVersion = '7226459'
def localLibDir = "${System.getProperty('user.home')}${dirChar}Desktop${dirChar}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    test.kotlin.srcDirs += 'src/test/kotlin'
    test {
        compileClasspath = main.output + main.resources
        runtimeClasspath = main.output + main.resources
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io/'}
    flatDir { dir("${localLibDir}JagTag0.5") }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.14'
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "net.dv8tion:JDA:$jdaVersion"
    compile 'com.github.JDA-Applications:Kotlin-JDA:56a6c22'
    //compile "com.jagrosh:JDA-Utilities:$jdaUtilsVersion"
    compile "com.github.JDA-Applications:JDA-Utilities:$jdaUtilsVersion"

    compile 'com.h2database:h2:1.3.148'

    compile 'org.jsoup:jsoup:1.10.2'
    compile 'com.google.apis:google-api-services-youtube:v3-rev183-1.22.0'
    compile 'com.github.TheMonitorLizard:je621:0.2'

    compile group: 'com.jagrosh', name: 'JagTag', version: '0.5'

    compileOnly([group: 'com.jagrosh', name: 'JagTag', version: '0.5', classifier: 'javadoc'],
            [group: 'com.jagrosh', name: 'JagTag', version: '0.5', classifier: 'sources'])
}

shadowJar {
    baseName = 'NightFury'
    version = ''
    classifier = ''
    manifest {
        attributes 'Implementation-Version': version, 'Main-Class': mainClassName
    }
}

build {
    dependsOn clean
    dependsOn classes
    dependsOn shadowJar

    classes.mustRunAfter clean
    shadowJar.mustRunAfter classes
}

kotlin.experimental.coroutines 'enable'

compileJava.options.encoding = 'UTF-8'
compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestJava.options.encoding = 'UTF-8'
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"