/*
 * Copyright 2017 Kaidan Gustave
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
class Version {
    String major, minor, build

    @Override
    String toString() {
        "${major}.${minor}.${this.build}"
    }
}

buildscript {
    ext.kotlin_version = '1.2.0-rc-39'
    ext.kotlinx_version = '0.19.3'
    ext.shadow_version = '2.0.1'
    ext.jda_version = '1fe4e3c'
    ext.jda_utilities_version = 'f9a0402'
    ext.lavaplayer_version = '1.2.44'
    ext.h2_version = '1.4.196'
    ext.logback_version = '1.2.3'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

def versionObj = new Version(major: 0, minor: 8, build: 2)
group = 'xyz.nightfury'
version = versionObj.toString()
mainClassName = "${group}.${rootProject.name}Kt"

sourceSets {
    test.kotlin.srcDirs += 'src/test/kotlin'
    test {
        compileClasspath = main.output + main.resources
        runtimeClasspath = main.output + main.resources
    }
}

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_version"

    // JDA
    compile "com.github.Dv8FromTheWorld:JDA:$jda_version"
    compile "com.github.JDA-Applications:JDA-Utilities:$jda_utilities_version"
    compile "com.sedmelluq:lavaplayer:$lavaplayer_version"

    // API Requests
    compile 'org.jsoup:jsoup:1.10.2'
    compile 'com.google.apis:google-api-services-youtube:v3-rev183-1.22.0'

    // h2
    compile "com.h2database:h2:$h2_version"

    // Logging
    compile "ch.qos.logback:logback-classic:$logback_version"

    // JagTag
    compile 'com.jagrosh:JagTag:0.5'
}

jar {
    manifest {
        attributes 'Implementation-Version': version.toString(), 'Main-Class': mainClassName
    }
}

shadowJar {
    baseName = 'NightFury'
    version = ''
    classifier = ''

    exclude('com/jagrosh/jdautilities/commandclient/**')
    exclude('android/**')

    manifest {
        attributes 'Manifest-Version': "${versionObj.major}.${versionObj.minor}"
    }
}

allprojects {
    group = 'xyz.nightfury'
    version = versionObj.toString()

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        maven { url 'https://jitpack.io/' }
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
    }

    dependencies {
        // Kotlin
        compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
        compileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_version"

        compileOnly "com.github.Dv8FromTheWorld:JDA:$jda_version"

        // Logging
        compileOnly "ch.qos.logback:logback-classic:$logback_version"

        // Testing
        testCompile 'junit:junit:4.12'
    }

    kotlin {
        experimental.coroutines = 'ENABLE'
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    build {
        dependsOn clean
        dependsOn classes
        dependsOn shadowJar

        classes.mustRunAfter clean
        shadowJar.mustRunAfter classes
    }
}

wrapper {
    gradleVersion '4.0'
}

build {
    doFirst {
        rootProject.childProjects.values().forEach { it.build }
    }
}